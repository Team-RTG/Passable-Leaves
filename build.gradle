buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
apply from: 'build-common.gradle'

sourceCompatibility = targetCompatibility = 1.8

group = project.package_base
archivesBaseName = "${project.name}" + "-" + (project.mc_version as String)
version = project.mod_version
def coremodClassifier = "coremod"

task coreJar(type: Jar) {
    from(sourceSets.main.output) {
        include "teamrtg/passableleaves/asm/**"
    }
    classifier coremodClassifier
    manifest {
        attributes ([
            "FMLCorePlugin": "teamrtg.passableleaves.asm.PassableLeavesCore",
            "Maven-Artifact": "${project.group}:${archivesBaseName}-${coremodClassifier}:${project.version}"
        ])
    }
}

jar {
    from(coreJar.archivePath.absolutePath) {
        include "*"
    }
    exclude "teamrtg/passableleaves/asm/**"
    manifest {
        attributes([
            "FMLAT": "passableleaves_at.cfg",
            "ContainedDeps": coreJar.archivePath.name,
            "Maven-Artifact": "${project.group}:${archivesBaseName}:${project.version}"
        ])

    }
    dependsOn coreJar
}

minecraft {
    version = project.mcf_version
    mappings = project.mcp_mappings
    runDir = "run"
    makeObfSourceJar = false
    replace "@MOD_VERSION@", project.version
    replace "@MC_VERSION@", project.mc_version
    replace "0.0-MCF+MINVER", project.mcf_minver
    replace "9001.0-MCF+MAXVER", project.mcf_maxver
    replaceIn "PassableLeaves.java"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand ([
            "modid"       : project.mod_id,
            "name"        : project.mod_name,
            "version"     : project.mod_version,
            "mcversion"   : project.mc_version,
        ])
    }
    from(sourceSets.main.resources.srcDirs) {exclude "mcmod.info"}
    rename "(.+_at.cfg)", "META-INF/\$1"
}
